__author__ = "Martin Rippin, Arielle R Munters"
__copyright__ = "Copyright 2022, Martin Rippin, Arielle R Munters"
__email__ = "martin.rippin@igp.uu.se, arielle.munters@scilifelab.uu.se"
__license__ = "GPL-3"


include: "rules/common.smk"
include: "rules/cnvkit_table.smk"
include: "rules/manta_to_tsv.smk"
include: "rules/mutectcaller_to_tsv.smk"
include: "rules/sample_order_multiqc.smk"


ruleorder: cnv_sv_manta_run_workflow_tn > misc_tabix
ruleorder: cnv_sv_manta_run_workflow_tn > misc_bgzip
ruleorder: _results_pindel_tbi > misc_tabix
ruleorder: _results_manta_tn_tbi > misc_tabix
ruleorder: _results_manta_tn_tbi_all > misc_tabix
ruleorder: _results_manta_tn_tbi_aml > misc_tabix
ruleorder: _results_tbi_tn > misc_tabix
ruleorder: _results_tbi_all > misc_tabix
ruleorder: _results_tbi_aml > misc_tabix
ruleorder: _results_tbi_t > misc_tabix
ruleorder: _results_cnvkit_tbi > misc_tabix
ruleorder: _results_crai > misc_samtools_index


rule all:
    input:
        unpack(compile_output_list),


module annotation:
    snakefile:
        github(
            "hydra-genetics/annotation",
            path="workflow/Snakefile",
            tag=config["modules"]["annotation"],
        )
    config:
        config


use rule * from annotation as annotation_*


use rule vep from annotation as annotation_vep with:
    input:
        vcf="parabricks/pbrun_mutectcaller_tn/{sample}.vcf.gz",
        tabix="parabricks/pbrun_mutectcaller_tn/{sample}.vcf.gz.tbi",
        cache=config["vep"]["vep_cache"],
        fasta=config["reference"]["fasta"],
    output:
        vcf=temp("parabricks/pbrun_mutectcaller_tn/{sample}.vep.vcf"),
    log:
        "parabricks/pbrun_mutectcaller_tn/{sample}.vep.vcf.log",
    benchmark:
        repeat(
            "parabricks/pbrun_mutectcaller_tn/{sample}.vep.vcf.benchmark.tsv",
            config.get("vep", {}).get("benchmark_repeats", 1),
        )
    message:
        "{rule}: Annotate {wildcards.sample}.vcf"


use rule vep from annotation as annotation_vep_t with:
    input:
        vcf="parabricks/pbrun_mutectcaller_t/{sample}_T.vcf.gz",
        tabix="parabricks/pbrun_mutectcaller_t/{sample}_T.vcf.gz.tbi",
        cache=config["vep"]["vep_cache"],
        fasta=config["reference"]["fasta"],
    output:
        vcf=temp("parabricks/pbrun_mutectcaller_t/{sample}_T.vep.vcf"),
    log:
        "parabricks/pbrun_mutectcaller_t/{sample}_T.vep.vcf.log",
    benchmark:
        repeat(
            "parabricks/pbrun_mutectcaller_t/{sample}_T.vep.vcf.benchmark.tsv",
            config.get("vep", {}).get("benchmark_repeats", 1),
        )
    message:
        "{rule}: Annotate {wildcards.sample}_T.vcf"


use rule simple_sv_annotation from annotation as annotation_simple_sv_annotation with:
    input:
        vcf="cnv_sv/manta_run_workflow_tn/{sample}/results/variants/somaticSV.snpeff.vcf.gz",
        panel=config["simple_sv_annotation"]["panel"],
        fusion_pairs=config["simple_sv_annotation"]["fusion_pairs"],
    output:
        vcf=temp("cnv_sv/manta_run_workflow_tn/{sample}.ssa.vcf"),
    log:
        "cnv_sv/manta_run_workflow_tn/{sample}.ssa.vcf.log",
    benchmark:
        repeat(
            "cnv_sv/manta_run_workflow_tn/{sample}.ssa.vcf.benchmark.tsv",
            config.get("snpeff", {}).get("benchmark_repeats", 1),
        )
    message:
        "{rule}: Annotate cnv_sv/manta_run_workflow_tn/{wildcards.sample}/results/variants/somaticSV.snpeff.vcf.gz"


module cnv_sv:
    snakefile:
        github(
            "hydra-genetics/cnv_sv",
            path="workflow/Snakefile",
            tag=config["modules"]["cnv_sv"],
        )
    config:
        config


use rule * from cnv_sv as cnv_sv_*


use rule cnvkit_batch from cnv_sv as cnv_sv_cnvkit_batch with:
    input:
        bam="parabricks/pbrun_fq2bam/{sample}_T.bam",
        bai="parabricks/pbrun_fq2bam/{sample}_T.bam.bai",
        cnv_reference=config["cnvkit_batch"]["normal_reference"],


use rule cnvkit_call from cnv_sv as cnv_sv_cnvkit_call with:
    input:
        segment="cnv_sv/cnvkit_batch/{sample}/{sample}_{type}.cns",
        vcf="parabricks/pbrun_mutectcaller_t/{sample}_{type}.vep.filter.germline.vcf",
    output:
        segment=temp("cnv_sv/cnvkit_call/{sample}_{type}.loh.cns"),


use rule cnvkit_scatter from cnv_sv as cnv_sv_cnvkit_scatter with:
    input:
        segments="cnv_sv/cnvkit_call/{sample}_{type}.loh.cns",
        segment_regions="cnv_sv/cnvkit_batch/{sample}/{sample}_{type}.cnr",
        vcf="parabricks/pbrun_mutectcaller_t/{sample}_{type}.vep.filter.germline.vcf",
    output:
        plot=temp("cnv_sv/cnvkit_scatter/{sample}_{type}_{locus}.png"),
    log:
        "cnv_sv/cnvkit_scatter/{sample}_{type}_{locus}.log",
    benchmark:
        repeat(
            "cnv_sv/cnvkit_scatter/{sample}_{type}_{locus}.benchmark.tsv",
            config.get("cnvkit_scatter", {}).get("benchmark_repeats", 1),
        )
    message:
        "{rule}: Plot cnvs into cnv_sv/cnvkit_scatter/{wildcards.sample}_{wildcards.type}_{wildcards.locus}.png"


use rule cnvkit_scatter from cnv_sv as cnv_sv_cnvkit_scatter_whole with:
    input:
        segments="cnv_sv/cnvkit_call/{sample}_{type}.loh.cns",
        segment_regions="cnv_sv/cnvkit_batch/{sample}/{sample}_{type}.cnr",
        vcf="parabricks/pbrun_mutectcaller_t/{sample}_{type}.vep.filter.germline.vcf",
    output:
        plot=temp("cnv_sv/cnvkit_scatter/{sample}_{type}.png"),
    params:
        extra=config.get("cnvkit_scatter_whole", {}).get("extra", ""),
    log:
        "cnv_sv/cnvkit_scatter/{sample}_{type}.log",
    benchmark:
        repeat(
            "cnv_sv/cnvkit_scatter/{sample}_{type}.benchmark.tsv",
            config.get("cnvkit_scatter_whole", {}).get("benchmark_repeats", 1),
        )


use rule config_manta_tn from cnv_sv as cnv_sv_config_manta_tn with:
    input:
        bam_t="parabricks/pbrun_fq2bam/{sample}_T.bam",
        bai_t="parabricks/pbrun_fq2bam/{sample}_T.bam.bai",
        bam_n="parabricks/pbrun_fq2bam/{sample}_N.bam",
        bai_n="parabricks/pbrun_fq2bam/{sample}_N.bam.bai",
        ref=config["reference"]["fasta"],


use rule generate_pindel_config from cnv_sv as cnv_sv_generate_pindel_config with:
    input:
        bam="parabricks/pbrun_fq2bam/{sample}_T.bam",
        metrics="qc/picard_collect_multiple_metrics/{sample}_T.insert_size_metrics",


use rule manta_run_workflow_tn from cnv_sv as cnv_sv_manta_run_workflow_tn with:
    input:
        bam_t="parabricks/pbrun_fq2bam/{sample}_T.bam",
        bai_t="parabricks/pbrun_fq2bam/{sample}_T.bam.bai",
        bam_n="parabricks/pbrun_fq2bam/{sample}_N.bam",
        bai_n="parabricks/pbrun_fq2bam/{sample}_N.bam.bai",
        ref=config["reference"]["fasta"],
        scrpt="cnv_sv/manta_run_workflow_tn/{sample}/runWorkflow.py",
    output:
        som_sv_vcf=temp("cnv_sv/manta_run_workflow_tn/{sample}/results/variants/somaticSV.vcf.gz"),
        som_sv_tbi=temp("cnv_sv/manta_run_workflow_tn/{sample}/results/variants/somaticSV.vcf.gz.tbi"),


use rule pindel_call from cnv_sv as cnv_sv_pindel_call with:
    input:
        bam="parabricks/pbrun_fq2bam/{sample}_T.bam",
        bai="parabricks/pbrun_fq2bam/{sample}_T.bam.bai",
        config="cnv_sv/pindel/{sample}.cfg",
        ref=config["reference"]["fasta"],


module compression:
    snakefile:
        github(
            "hydra-genetics/compression",
            path="workflow/Snakefile",
            tag=config["modules"]["compression"],
        )
    config:
        config


use rule * from compression as compression_*


use rule samtools_view from compression as compression_samtools_view with:
    input:
        bam="parabricks/pbrun_fq2bam/{file}.bam",
        bai="parabricks/pbrun_fq2bam/{file}.bam.bai",
        ref=config.get("reference", {}).get("fasta", ""),


module filtering:
    snakefile:
        github(
            "hydra-genetics/filtering",
            path="workflow/Snakefile",
            tag=config["modules"]["filtering"],
        )
    config:
        config


use rule * from filtering as filtering_*


module parabricks:
    snakefile:
        github(
            "hydra-genetics/parabricks",
            path="workflow/Snakefile",
            tag=config["modules"]["parabricks"],
        )
    config:
        config


use rule * from parabricks as parabricks_*


module prealignment:
    snakefile:
        github(
            "hydra-genetics/prealignment",
            path="workflow/Snakefile",
            tag=config["modules"]["prealignment"],
        )
    config:
        config


use rule * from prealignment as prealignment_*


module qc:
    snakefile:
        github("hydra-genetics/qc", path="workflow/Snakefile", tag=config["modules"]["qc"])
    config:
        config


use rule * from qc as qc_*


use rule mosdepth from qc as qc_mosdepth with:
    input:
        bam="parabricks/pbrun_fq2bam/{sample}_{type}.bam",
        bai="parabricks/pbrun_fq2bam/{sample}_{type}.bam.bai",


use rule multiqc from qc as qc_multiqc with:
    input:
        files=lambda wildcards: set(
            [
                file.format(sample=sample, type=u.type, lane=u.lane, flowcell=u.flowcell, barcode=u.barcode, read=read, ext=ext)
                for file in config["multiqc"]["reports"][wildcards.report]["qc_files"]
                for sample in get_samples(samples)
                for u in units.loc[sample].dropna().itertuples()
                if u.type in config["multiqc"]["reports"][wildcards.report]["included_unit_types"]
                for read in ["fastq1", "fastq2"]
                for ext in config.get("picard_collect_multiple_metrics", {}).get("output_ext", [""])
            ]
        ),
        config=lambda wildcards: config["multiqc"]["reports"][wildcards.report]["config"],
        sample_replacement="qc/multiqc/sample_replacement.tsv",
        sample_order="qc/multiqc/sample_order.tsv",
    params:
        extra=lambda wildcards, input: "--replace-names "
        + input.sample_replacement
        + " --sample-names "
        + input.sample_order
        + " -c "
        + input.config,


use rule picard_collect_alignment_summary_metrics from qc as qc_picard_alignment_summary_metrics with:
    input:
        bam="parabricks/pbrun_fq2bam/{sample}_{type}.bam",
        ref=config["reference"]["fasta"],


use rule picard_collect_duplication_metrics from qc as qc_picard_duplication_metrics with:
    input:
        bam="parabricks/pbrun_fq2bam/{sample}_{type}.bam",


use rule picard_collect_gc_bias_metrics from qc as qc_picard_collect_gc_bias_metrics with:
    input:
        bam="parabricks/pbrun_fq2bam/{sample}_{type}.bam",
        ref=config["reference"]["fasta"],


use rule picard_collect_hs_metrics from qc as qc_picard_collect_hs_metrics with:
    input:
        bam="parabricks/pbrun_fq2bam/{sample}_{type}.bam",
        bait_intervals=config["reference"]["design_intervals"],
        reference=config["reference"]["fasta"],
        target_intervals=config["reference"]["design_intervals"],


use rule picard_collect_multiple_metrics from qc as qc_picard_collect_multiple_metrics with:
    input:
        bam="parabricks/pbrun_fq2bam/{sample}_{type}.bam",
        ref=config["reference"]["fasta"],


use rule picard_collect_wgs_metrics from qc as qc_picard_collect_wgs_metrics with:
    input:
        bam="parabricks/pbrun_fq2bam/{sample}_{type}.bam",
        ref=config["reference"]["fasta"],
        interval=config["reference"]["wgs_intervals"],


use rule samtools_stats from qc as qc_samtools_stats with:
    input:
        bam="parabricks/pbrun_fq2bam/{sample}_{type}.bam",


module misc:
    snakefile:
        github("hydra-genetics/misc", path="workflow/Snakefile", tag=config["modules"]["misc"])
    config:
        config


use rule * from misc as misc_*


use rule samtools_index from misc as misc_samtools_index with:
    input:
        cram="{file}.crumble.cram",
    output:
        crai=temp("{file}.crumble.cram.crai"),
    params:
        extra=config.get("extra", {}).get("extra", ""),
    log:
        "{file}.crumble.cram.crai.log",
    benchmark:
        repeat(
            "{file}.crumble.cram.crai.benchmark.tsv",
            config.get("samtools_index", {}).get("benchmark_repeats", 1),
        )
    message:
        "{rule}: Index {wildcards.file}.crumble.cram file"
